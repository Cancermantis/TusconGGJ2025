@tool
extends Area3D
class_name SoundBroadcaster

# Provides a simplified system for notifying nearby objects of sounds generated by an object.
# Base volume can be driven programatically by an owning object to simulate changes in how loud 
# the owning object is. Other objects interacting with the owner can add or subtract a percentage-
# based multiplier to increase the effective volume of the broadcaster

# abstract base value representing how "loud" the broadcaster is being.
# Louder sound broacasters are more likely to signal sound recievers at greater distances
@export var base_volume: float = 0.0
# Coefficient describing how effective volume decreases over distance from the broadcaster's origin
@export var attenuation: float = 1.0
@export var area: CylinderShape3D:
	get:
		return area
	set(value):
		area = value
		if(collision != null):
			collision.shape = area
# percentage-increase applied to volume for the purposes of calculating the effective range of sound
# Example: a value of 0.5 multiplies base_volume by 1.5, a value of 2.0 by 3.0, etc
var percent_additive: float = 0.0

var collision: CollisionShape3D
var receivers: Array[SoundReceiver]

# Called when the node enters the scene tree for the first time.
func _ready() -> void:
	collision = $CollisionShape3D
	if(collision != null):
		collision.shape = area

# Called every frame. 'delta' is the elapsed time since the previous frame.
func _physics_process(delta: float) -> void:
	if(Engine.is_editor_hint() || area == null):
		return
	
	var volume = base_volume * (1.0 + percent_additive)
	for receiver in receivers:
		var distance = receiver.global_position.distance_to(global_position)
		var ratio = 1.0 - ( distance / area.radius )
		ratio = pow(ratio, attenuation)
		receiver._check_trigger(volume * ratio)

func _on_body_entered(body: Node3D) -> void:
	if(Engine.is_editor_hint()):
		return
	
	var receiver = body.find_child("SoundReceiver")
	if(receiver != null):
		receivers.append(receiver)

func _on_body_exited(body: Node3D) -> void:
	if(Engine.is_editor_hint()):
		return
	
	var receiver = body.find_child("SoundReceiver")
	if(receiver != null):
		receivers.erase(receiver)
